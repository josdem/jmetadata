plugins {
    id 'java'
    id 'application'
    id 'jacoco'
    id 'org.sonarqube' version '6.0.0.5145'
    id 'com.diffplug.spotless' version '7.0.2'
    id 'org.springframework.boot' version '3.4.1'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.jetbrains.kotlin.jvm' version '2.1.20'
}

group = 'com.josdem.jmetadata'
version = '1.4.4'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

kotlin {
    jvmToolchain(21)
}

application {
    mainClass = 'com.josdem.jmetadata.Launcher'
}

ext {
    audioTaggerVersion = '2.0.3'
    retrofitVersion = '2.11.0'
    junitVersion = '5.11.4'
    aspectjVersion = '1.9.22.1'
    messengineVersion = '0.1.0'
    festSwingVersion = '1.2.1'
    javaXmlBindVersion = '2.3.1'
    javaAnnotationVersion = '1.3.2'
    commonsLangVersion = '3.17.0'
    mockitoVersion = '3.12.4'
    mockitoInlineVersion = '5.2.0'
    targetDir = "src/main/java/com/josdem/jmetadata"
    mockitoKotlinVersion = "5.4.0"
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

spotless {
    java {
        target fileTree('.') {
            include 'src/**/*.java'
            exclude '/build/**'
        }
        googleJavaFormat()
        removeUnusedImports()
        endWithNewline()
    }
    kotlin {
        target fileTree('.') {
            include 'src/**/*.kt'
            exclude '/build/**'
        }
        ktlint()
        trimTrailingWhitespace()
        endWithNewline()
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation "org.aspectj:aspectjtools:$aspectjVersion"
    implementation "org:jaudiotagger:$audioTaggerVersion"
    implementation "org.asmatron:messengine:$messengineVersion"
    implementation "org.apache.commons:commons-lang3:$commonsLangVersion"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "javax.xml.bind:jaxb-api:$javaXmlBindVersion"
    implementation "javax.annotation:javax.annotation-api:$javaAnnotationVersion"
    implementation "org.easytesting:fest-swing:$festSwingVersion"
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "org.mockito:mockito-inline:$mockitoInlineVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation "org.mockito.kotlin:mockito-kotlin:$mockitoKotlinVersion"
    implementation files('lib/last.fm-bindings-0.1.0.jar')
}

jacoco {
    toolVersion = "0.8.12"
}

jacocoTestReport {
    dependsOn test

    reports {
        html.required = true
        xml.required = true
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['**/laf/**/*', '**/gui/**/*', '**/dnd/**/*'])
        }))
    }

}

test {
    useJUnitPlatform()
}

sonar {
    properties {
        property "sonar.projectKey", "josdem_jmetadata"
        property "sonar.organization", "josdem-io"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

test.dependsOn("spotlessApply")

tasks.register('settingCredentials') {
    ant.replace(file: "${targetDir}/Auth.java", token: "LASTFM_API_KEY", value: "${LASTFM_API_KEY}")
    ant.replace(file: "${targetDir}/Auth.java", token: "LASTFM_API_SECRET", value: "${LASTFM_API_SECRET}")
}